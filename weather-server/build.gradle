plugins {
    id 'com.bmuschko.docker-java-application' version '7.1.0'
}

dependencies {
    implementation(project(":weather-api"))
    implementation 'io.dropwizard:dropwizard-core:2.0.24'
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
    jacoco {
        excludes += ['**/grpc']
    }
}

docker {
    javaApplication {
        baseImage = 'openjdk:16-slim'
        ports = [8080,8081,50051]
        images = [
                "r0mahat/weather-service:latest",
                "r0mahat/weather-service:${project.version}"
        ]
        jvmArgs = ['-Djava.security.egd=file:/dev/./urandom', '-XX:MaxRAMPercentage=70.0']
    }
    registryCredentials {
        username = System.getenv('DOCKERHUB_USER')
        password = System.getenv('DOCKERHUB_TOKEN')
    }
}

tasks.compileJava.finalizedBy(tasks.dockerBuildImage)
tasks.publish.finalizedBy(tasks.dockerPushImage)